
# The two-dimensional mesh will be defined on a surface of
# this brick
brick x 1 y 0.5 z 0.1
volume 1 name "mybrick"
list names volume

# This will make all coordinates positive. Not necessary
# but can be convenient. Cubit uses symmetrical coordinates by default.
move vertex 4 location 0 0 0 nomerge

# Cut the volume in half to refine the pn junction region
webcut volume 1 with plane xplane offset 0.5 noimprint merge
merge all

# Define the nodesets that will be the electrical contacts
# for the simulations (Albany currently supports only nodeset)
nodeset 1 curve 3
nodeset 1 name "anode"
nodeset 2 curve 1
nodeset 2 name "cathode"

# Define blocks for a single material
block 1 surface 15
block 1 name "silicon.ptype"
block 1 element type quad4

block 2 surface 8
block 2 name "silicon.ntype"
block 2 element type quad4

# Define meshing size on the long side
curve 26 18 interval 50
curve 26 18 scheme equal

# Define meshing size on the short side
curve 3 interval 10
curve 3 scheme equal

# Actually mesh the geometry using the default meshing engine (Map)
surface 15 8 scheme map
mesh surface all

# Merge all meshing elements
merge all

# Output the Exodus II mesh
export mesh "pndiode.exo" dimension 2 block all overwrite



