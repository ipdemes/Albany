#     {side = 20}
#     {wgate = 8}
#     {lgate = 8}

# vertices for Silicon substrate
create vertex {-side/2.0} {-side/2.0} 0
create vertex {side/2.0} {-side/2.0} 0
create vertex {side/2.0} {side/2.0} 0
create vertex {-side/2.0} {side/2.0} 0

# vertices for point contact gate regions
create vertex {-side/2.0} {-wgate/2.0} 0
create vertex {-side/2.0} { wgate/2.0} 0
create vertex {-side/2.0 + lgate} { 0.5/2.0} 0
create vertex {-side/2.0 + lgate} { -0.5/2.0} 0

create vertex {side/2.0} {-wgate/2.0} 0
create vertex {side/2.0} { wgate/2.0} 0
create vertex {side/2.0 - lgate} { 0.5/2.0} 0
create vertex {side/2.0 - lgate} { -0.5/2.0} 0

# create the Silicon region (surface 1)
create surface vertex 1 2 3 4

# create the SiO2 gate 1 region (surface 2)
create surface vertex 5 6 7 8

# create the SiO2 gate 2 region (surface 3)
create surface vertex 9 10 11 12

# merge all overlapped curves
#merge all

#remove portion of Si02 where gates are 
#(destroys surfaces 1,2,3, creates 5)
subtract 2 from 1
subtract 3 from 1

# re-create the gate regions
create surface vertex 13 14 15 16
create surface vertex 17 18 19 20

# merge all overlapped curves
merge all

### define nodesets for BCs
nodeset 1 curve in surface 6
nodeset 1 name "lgate"
color nodeset 1 red

nodeset 2 curve in surface 7
nodeset 2 name "rgate"
color nodeset 2 red

nodeset 3 curve 1
nodeset 3 name "bottom"
color nodeset 3 blue

nodeset 4 curve 3
nodeset 4 name "top"
color nodeset 4 green


## define blocks (each block represents one material)
block 1 surface 5
block 1 name "silicon"
block 1 element type tri3

block 2 surface 6 7
block 2 name "sio2"
block 2 element type tri3

# set mesh size
surface all size 0.5
surface all scheme trimesh


#mesh surfaces
mesh surface all

# dimension size is assumed in [um]
# output the Exodus II mesh
export mesh "pointcontact_2D.exo" dimension 2 block all overwrite
