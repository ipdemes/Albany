#     {wsub = 0.8}
#     {tsub = 2}
#     {tox = 0.035}
#     {tpoly = 0.2}
#     {wpoly = 0.8}


# vertices for Silicon substrate
create vertex {-wsub/2.0} 0 0 
create vertex {wsub/2.0} 0 0
create vertex {wsub/2.0} {tsub} 0
create vertex {-wsub/2.0} {tsub} 0

# vertices for SiO2 region
create vertex {-wsub/2.0} {-tox} 0
create vertex {wsub/2.0} {-tox} 0

# create the Silicon region
create surface vertex 1 2 3 4

# create the SiO2 region
create surface vertex 1 5 6 2

# create the PolySilicon gate region
create vertex {-wpoly/2.0} {-tox-tpoly} 0
create vertex {wpoly/2.0} {-tox-tpoly} 0
create vertex {wpoly/2.0} {-tox} 0
create vertex {-wpoly/2.0} {-tox} 0

create surface vertex 9 10 11 12 
imprint all

# merge all overlapped curves
merge all

# define nodesets for BCs
nodeset 1 curve 3
nodeset 1 name "psub"
color nodeset 1 blue

# gate on top of polysilicon
nodeset 2 curve 9
nodeset 2 name "polygate"
color nodeset 1 red

# define blocks (each block represents one material)
block 1 surface 1
block 1 name "silicon.psub"
block 1 element type quad4

block 2 surface 2
block 2 name "sio2"
block 2 element type quad4

block 3 surface 3
block 3 name "polysi.gate"
block 3 element type quad4

# define meshing size
surface 1 2 3 size 0.05

# actually mesh the geometry using the default meshing engine (Map)
mesh surface all

# refine mesh in Silicon near the Silicon/SiO2 interface
refine curve 1 numsplit 2 bias 1.0 depth 2 smooth
refine curve 1 numsplit 1 bias 1.0 depth 1 smooth

# refine mesh in PolySilicon near the Poly/SiO2 interface
refine curve 6 numsplit 1 bias 1.0 depth 1 smooth

# merge all overlapped elements
merge all

# dimension size is assumed in [um]
# output the Exodus II mesh
export mesh "pmoscap_polygate.exo" dimension 2 block all overwrite


