#     {wsub = 20}
#     {tsub = 10}
#     {tox = 3}
#     {wgate = 5}
#     {tgate = 1}
#     {tgateox = 1}

# vertices for Silicon substrate
create vertex {-wsub/2.0} {-tsub} 0
create vertex {wsub/2.0} {-tsub} 0
create vertex {wsub/2.0} 0 0
create vertex {-wsub/2.0} 0 0

# vertices for SiO2 region
create vertex {-wsub/2.0} {tox} 0
create vertex {wsub/2.0} {tox} 0

# vertices for gate region
create vertex {-wsub/2.0} {tgateox} 0
create vertex {-wsub/2.0 + wgate} {tgateox} 0
create vertex {-wsub/2.0 + wgate} {tgate+tgateox} 0
create vertex {-wsub/2.0} {tgate+tgateox} 0

create vertex {wsub/2.0-wgate} {tgateox} 0
create vertex {wsub/2.0} {tgateox} 0
create vertex {wsub/2.0} {tgate+tgateox} 0
create vertex {wsub/2.0-wgate} {tgate+tgateox} 0

# create the Silicon region (surface 1)
create surface vertex 1 2 3 4

# create the SiO2 region (surface 2)
create surface vertex 3 4 5 6

# create the left gate region (surface 3)
create surface vertex 7 8 9 10

# create the right gate region (surface 4)
create surface vertex 11 12 13 14

# merge all overlapped curves
merge all

#remove portion of Si02 where left gate is 
#(destroys surface 3, creates 5)
subtract 3 from 2

#remove portion of Si02 where right gate is 
#(destroys surface 3, creates 6)
subtract 4 from 2


# re-create the gate regions
create surface vertex 17 18 19 20
create surface vertex 21 22 23 24

# merge all overlapped curves
merge all

# define nodesets for BCs
nodeset 1 curve 17 20 21 28
nodeset 1 name "lgate"
color nodeset 1 red

nodeset 2 curve 22 23 24 34
nodeset 2 name "rgate"
color nodeset 1 red

nodeset 3 curve 1
nodeset 3 name "substrate"
color nodeset 1 blue

nodeset 4 curve 7
nodeset 4 name "topgate"
color nodeset 1 green

# define blocks (each block represents one material)
block 1 surface 1
block 1 name "silicon"
block 1 element type tri6

block 2 surface 6
block 2 name "sio2"
block 2 element type tri6

block 3 surface 7 8
block 3 name "polysilicon"
block 3 element type tri6

# set mesh size
surface 1 6 7 8 size 1
surface 1 6 7 8 scheme trimesh

#mesh surfaces
mesh surface all

# refine mesh in Silicon near the Silicon/SiO2 interface
# one refinement generates 1800 elements
refine curve 3 numsplit 1 bias 1.2 depth 3 smooth
#refine curve 13 numsplit 1 bias 1.2 depth 5 smooth

# merge all overlapped elements
merge all

# dimension size is assumed in [um]
# output the Exodus II mesh
export mesh "mosdot_2D_tri6.exo" dimension 2 block all overwrite






