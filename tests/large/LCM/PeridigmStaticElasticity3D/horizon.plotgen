
x_1 = -2.0        # left end, start of first linear segment
x_2 = -1.5        # end of first linear segment, start of sinusoid
x_3 = -0.5        # end of sinusoid, start of second linear segment
x_4 =  0.5        # end of second linear segment, start of sinusoid
x_5 =  1.5        # end of sinusoid, start of third linear segment
x_6 =  2.0        # end of third linear segment
h_min = 0.12  # minimum horizon
h_max = 0.20  # maximum horizon

# C1 is 1.5 * pi
C1 = 4.71238898038469
# C2 is 2.5 * pi
C2 = 7.85398163397448

plot_gap = 0.1*(h_max - h_min)

Horizon(x) = x < x_2 ? h_min : ( x < x_3 ? h_min + ( (sin(C1 + (x - x_2)*(C2 - C1)/(x_3 - x_2)) + 1.0)/2.0 )*(h_max - h_min) : x < x_4 ? h_max : ( x < x_5 ? h_max - ( (sin(C1 + (x - x_4)*(C2 - C1)/(x_5 - x_4)) + 1.0)/2.0 )*(h_max - h_min) : h_min ) )

set terminal pdf enhanced font "Times-Roman,24" size 12in, 8in 
set output "horizon.pdf"
set xlabel "Location (m)" font "Times-Roman,32"
set ylabel "Horizon Value" font "Times-Roman,32"
set key bottom right
set xrange [x_1:x_6]
set yrange [h_min-plot_gap:h_max+plot_gap]
plot Horizon(x) with linespoints lw 10 ps 1 pt 7 notitle



#  Format for Peridigm input deck
#
#  x &lt; -1.5 ? 0.12 : ( x &lt; -0.5 ? 0.12 + ( (sin(4.71238898038469 + (x - -1.5)*(7.85398163397448 - 4.71238898038469)/(-0.5 - -1.5)) + 1.0)/2.0 )*(0.20 - 0.12) : x &lt; 0.5 ? 0.20 : ( x &lt; 1.5 ? 0.20 - ( (sin(4.71238898038469 + (x - 0.5)*(7.85398163397448 - 4.71238898038469)/(1.5 - 0.5)) + 1.0)/2.0 )*(0.20 - 0.12) : 0.12 ) )

